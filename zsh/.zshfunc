# .zshfunc

### smart cd function from grml, allows switching to /etc when running 'cd /etc/fstab'
function cd () {
	if (( ${#argv} == 1 )) && [[ -f ${1} ]]; then
		[[ ! -e ${1:h} ]] && return 1
		print "Correcting ${1} to ${1:h}"
		builtin cd ${1:h}
	else
		builtin cd "$@"
	fi
}

### Show explicit installed packages not in base groups
# <https://wiki.archlinux.org/index.php/Pacman/Tips_and_tricks#Not_in_a_specified_group_or_repository>
pacexplicit() {
	if (( $+commands[fzf] )); then
		pacman -Qqe | grep -Fxv "$( ( pactree -u base; pacman -Qqg base-devel ) | sort -u ) " | fzf --layout=reverse-list --preview 'pacman -Qii {}' --preview-window=':75%:wrap'
	else
		pacman -Qqe | grep -Fxv "$( ( pactree -u base; pacman -Qqg base-devel ) | sort -u ) "
	fi
}

### Show all uninstalled optional dependencies
pacoptdeps() {
	grep -Gvf <(for i in $(pacman -Qq); do echo "^$i"; done) <(expac -Q -l'\n' %O | sort)
}

### simple notes
# <https://bbs.archlinux.org/viewtopic.php?pid=1812351#p1812351>
n() {
	local files
	files=(${@/#/$HOME/Notes/})
	${EDITOR:-vi} $files
}

# TAB completion for notes
_notes() {
	_files -g "*" -W $HOME/Notes
}
compdef _notes n

# list notes
nls() {
	tree -CR --dirsfirst --noreport $HOME/Notes | awk '{
	if (NF==1) print $1;
	else if (NF==2) print $2;
	else if (NF==3) printf "  %s\n", $3
	}'
}

### IIDX SUDDEN+ calculator
# <https://remywiki.com/CS_GOLD#General_Information_.2F_Changes>
suddenplus() {
	local green_number=300
	local bpm
	local hispeed

	if [[ -z $1 ]]; then
		echo "Usage: suddenplus (BPM) [Green number]"
		return 1
	fi
	bpm=$1

	if [[ $2 ]]; then
		green_number=$2
	fi

	echo "Green number: $green_number"
	echo "Hi-speed │ Lane cover"
	echo "─────────┼───────────"
	for hispeed in 1.0 1.5 2.0 2.5 3.0 3.5 4.0 4.5 5.0; do
		lane_cover=$(( 1000 - $hispeed * $green_number * $bpm / 174 ))
		if (( $lane_cover < 0 )); then
			echo "     $hispeed | Too fast!"
			break
		else
			echo "     $hispeed │ ${lane_cover%.*}"
		fi
	done

	unset green_number bpm hispeed lane_cover
}

### Magnet URL information extractor
# <https://unix.stackexchange.com/a/134286>
demagnetize() {
	if [[ $1 ]]; then
		hash=$(echo "$1" | grep -oP "(?<=btih:).*?(?=&)")
		echo "Magnet hash: $hash"
		aria2c --bt-metadata-only=true --bt-save-metadata=true -q "$1"
		aria2c "$hash.torrent" -S
		rm "$hash.torrent"
	else
		echo "Missing magnet URI"
		return 1
	fi
}

### Convert GIFs to MP4 files
# <http://rigor.com/blog/2015/12/optimizing-animated-gifs-with-html5-video>
gif2mp4() {
	ffmpeg -i "$1" -pix_fmt yuv420p -vf "crop=trunc(iw/2)*2:trunc(ih/2)*2" "${1/.gif/.mp4}"
}

### Rename files to their MD5 hash
md5rename() {
	[[ $1 ]] || { echo "Missing input file"; return 1 }
	local md5 ext
	md5=$(md5sum "$1" | cut -d' ' -f1)
	ext=${1##*.}
	mv -iv "$1" "$md5.$ext"
}

### Show Arch Linux package popularity stats
pkgpop() {
	if [[ ! $1 ]]; then
		cat << EOF
Usage: pkgpop [-g] package [packages...]

    -g | --graph : Open a web page with a popularity graph
EOF
	return 1
	fi

	local graph
	if [[ $1 == "-g" || $1 == "--graph" ]]; then
		graph=1
		shift
	fi

	pkgs=( $@ )
	if [[ $graph == 1 ]]; then
		IFS=, && xdg-open "https://pkgstats.archlinux.de/compare/packages#packages=${pkgs[*]}"
	else
		curl -s "https://pkgstats.archlinux.de/api/packages/${(@)^pkgs}" | jq -rs 'sort_by(-.count)[] | "\(.name): \t\(.popularity)\t%"' | column -ts $'\t' -o '' -R1,2
	fi
}

### ISO (un)mounting
iso-mount() {
	local MOUNTPOINT="$XDG_RUNTIME_DIR/fuseiso"
	[[ -d $MOUNTPOINT ]] || mkdir -p $MOUNTPOINT
	fuseiso $1 $MOUNTPOINT && cd $MOUNTPOINT
}

iso-unmount() {
	local MOUNTPOINT="$XDG_RUNTIME_DIR/fuseiso"
	[[ $PWD == $MOUNTPOINT ]] && cd $HOME
	fusermount -u $MOUNTPOINT
}

### SSHFS (un)mounting
ssh-mount() {
	local MOUNTPOINT="$XDG_RUNTIME_DIR/sshfs"
	[[ -d $MOUNTPOINT ]] || mkdir -p $MOUNTPOINT
	sshfs $1 $MOUNTPOINT && cd $MOUNTPOINT
}

ssh-unmount() {
	local MOUNTPOINT="$XDG_RUNTIME_DIR/sshfs"
	[[ $PWD == $MOUNTPOINT ]] && cd $HOME
	fusermount -u $MOUNTPOINT
}
